// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    
    field Array buffer;
    field int bufferLength;
    field int strLength;
 
    /** Constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        if (maxLength = 0) {
            let maxLength = 1;
        }
        let buffer = Array.new(maxLength);
        let bufferLength = maxLength;
        let strLength = 0;
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do Array.dispose(buffer);
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
      return strLength;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
      return buffer[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
      let buffer[j] = c;
      return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if (strLength < bufferLength) {
            let buffer[strLength] = c;
            let strLength = strLength + 1;
        }
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (strLength > 0) {
            let strLength = strLength - 1;
        }
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int value;
        var int i;
        var boolean neg;
        
        let value = 0;
        
        if ((strLength > 0) & (buffer[0] = 45)) {    // '-'
            let neg = true;
            let i = 1;
        }
        else {
            let neg = false;
            let i = 0;
        }
        
        while ((i < strLength) & String.isDigit(buffer[i])) {
            let value = (value * 10) + String.digitValue(buffer[i]);
            let i = i + 1;
        }
        
        if (neg) {
            return -value;
        }
        else {
            return value;
        }
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int value) {
        let strLength = 0;    // Clear string

        if (value < 0) {
            let value = -value;
            do appendChar(45);      // leading '-'
        }

        do recursiveSetInt(value);
        return;
    }

    method void recursiveSetInt(value) {
        var int q;
        var int mod;
        var char c;

        let q = value / 10;
        let mod = value - (q*10);
        let c = String.digit_char(mod);
        
        if (value < 10 ) {
            do appendChar(c);
        }
        else {
            do recursiveSetInt(q);
            do appendChar(c);
        }
        return;
    }

    /** Returns whether the given char is a digit or not */
    function boolean isDigit(char c) {
        return ~(c < 48) & ~(c > 57);
    }
    
    /** Returns the integer value of the given digit character */
    function int digitValue(char c) {
        return c - 48;
    }
    
    /** Returns the char value of the given integer (must have 0<=value<=9) */
    function char digitChar(int i) {
        return i + 48;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
