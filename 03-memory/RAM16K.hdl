// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/RAM16K.tst

/**
 * Memory of 16K 16-bit registers (16K registers, each 16 bit-wide).
 * If load is asserted (load=1), the value of the register selected by
 * address is set to in (that is, the in value is loaded into the memory
 * location specified by the address); Otherwise, the value does not change.
 * The value of the selected register is emitted by out
 * (after the next time step).
 */

CHIP RAM16K {
    IN in[16], load, address[14];
    OUT out[16];

    PARTS:
    DMux4Way(in=load, sel=address[12..13], a=a, b=b, c=c, d=d);
    RAM4K(in=in, load=a, address=address[0..11], out=outA);
    RAM4K(in=in, load=b, address=address[0..11], out=outB);
    RAM4K(in=in, load=c, address=address[0..11], out=outC);
    RAM4K(in=in, load=d, address=address[0..11], out=outD);
    Mux4Way16(a=outA, b=outB, c=outC, d=outD, sel=address[12..13], out=out);
}
